package gen

import (
	"list"
	"path"

  hof "github.com/hofstadter-io/hof/schema"

  "github.com/hofstadter-io/hofmod-server/schema"
)

#ServerGen: hof.#HofGenerator & {
	// User inputs
  Outdir: string | *"./"

  Server: schema.#Server
	let ServerInput = Server

	UserModels: hof.#Datamodel
	let UserModelsInput = UserModels

  // Internal
	BuiltinModels: hof.#Datamodel

	Datamodel: (#ModelGen & {
		Server: ServerInput
		UserModels: UserModelsInput
	}).Datamodel

  In: {
    SERVER: Server
		MODELS: {
			User: UserModels
			Builtin: BuiltinModels
		}
		ModuleImport: path.Clean("\(Module)/\(Outdir)")
  }

  OutdirConfig: {
    CiOutdir: string | *"\(Outdir)/ci/\(In.SERVER.serverName)"
    ServerOutdir: string | *"\(Outdir)/server"
  }

  basedir: "server/\(In.SERVER.serverName)"

	Module: string
  PackageName: "" | *"github.com/hofstadter-io/hofmod-server"

  PartialsDir:  "./partials/"
  TemplatesDir: "./templates/"

	// Actual files generated by hof, flattened into a single list
  Out: [...hof.#HofGeneratorFile] & list.FlattenN(All , 1)

  // Combine everything together and output files that might need to be generated
  All: [
   [ for _, F in OnceFiles { F } ],
   [ for _, F in Models { F } ],
   [ for _, F in L1_RouteFiles { F } ],
   [ for _, F in L2_RouteFiles { F } ],
   [ for _, F in L3_RouteFiles { F } ],
   [ for _, F in L1_ResourceFiles { F } ],
  ]

  // Files that are not repeatedly used, they are generated once for the whole CLI
  OnceFiles: [...hof.#HofGeneratorFile] & [
    {
      TemplateName: "config.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/config/config.go"
    },
    {
      TemplateName: "server.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/server.go"
    },
    {
      TemplateName: "router.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/router.go"
    },
    {
      TemplateName: "middleware.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/middleware.go"
    },
		{
			TemplateName: "auth/middleware.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/auth/middleware.go"
		},
		{
			TemplateName: "auth/routes.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/auth/routes.go"
		},
		{
			TemplateName: "auth/pword.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/auth/pword.go"
		},
		{
			TemplateName: "auth/apikey.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/auth/apikey.go"
		},
		{
			TemplateName: "db/common.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/db/common.go"
		},
		{
			TemplateName: "db/migrate.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/db/migrate.go"
		},
		{
			TemplateName: "db/seed.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/db/seed.go"
		},
		{
			TemplateName: "db/postgres.go"
			Filepath: "\(OutdirConfig.ServerOutdir)/db/postgres.go"
		},
  ]

	// No actual files here
  Modelsets: [...hof.#HofGeneratorFile] & list.FlattenN([[
    for _, M in Datamodel.Modelsets
    {
      In: {
        MODELSET: {
          M
        }
      }
		}
	]], 1)

  MPP: [ for P in Modelsets if len(P.In.MODELSET.Models) > 0 {
    [ for M in P.In.MODELSET.Models { M,  Parent: { Name: P.In.MODELSET.Name } }]
  }]
  Models: [...hof.#HofGeneratorFile] & [ // List comprehension
    for _, M in list.FlattenN(MPP, 1)
    {
      In: {
				MODEL: {
					M
          PackageName: "dm"
				}
      }
      TemplateName: "dm/model.go"
      Filepath: "\(Outdir)/dm/\(M.modelName).go"
    }
  ]

	// Routes
  L1_RouteFiles: [...hof.#HofGeneratorFile] & list.FlattenN([[
    for _, R in Server.Routes
    {
      In: {
        ROUTE: {
          R
          PackageName: "routes"
        }
      }
      TemplateName: "route.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/routes/\(In.ROUTE.Name).go"
		}
	]], 1)

  L2_RouteList: [ for P in L1_RouteFiles if len(P.In.ROUTE.Routes) > 0 {
    [ for R in P.In.ROUTE.Routes { R,  Parent: { Name: P.In.ROUTE.Name } }]
  }]
  L2_RouteFiles: [...hof.#HofGeneratorFile] & [ // List comprehension
    for _, R in list.FlattenN(L2_RouteList, 1)
    {
      In: {
				ROUTE: {
					R
          PackageName: R.Parent.Name
				}
      }
      TemplateName: "route.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/routes/\(R.Parent.Name)/\(R.Name).go"
    }
  ]

  L3_RouteList: [ for P in L2_RouteFiles if len(P.In.ROUTE.Routes) > 0 {
    [ for R in P.In.ROUTE.Routes { R,  Parent: { Name: P.In.ROUTE.Name, Parent: P.In.ROUTE.Parent } }]
  }]
  L3_RouteFiles: [...hof.#HofGeneratorFile] & [ // List comprehension
    for _, R in list.FlattenN(L3_RouteList, 1)
    {
      In: {
				ROUTE: {
					R
          PackageName: R.Parent.Name
				}
      }
      TemplateName: "route.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/routes/\(R.Parent.Parent.Name)/\(R.Parent.Name)/\(R.Name).go"
    }
  ]

	// Resource Routes
  L1_ResourceFiles: [...hof.#HofGeneratorFile] & list.FlattenN([[
    for _, R in Server.Resources
    {
      In: {
        RESOURCE: {
          R
          PackageName: "resources"
        }
      }
      TemplateName: "resource.go"
      Filepath: "\(OutdirConfig.ServerOutdir)/resources/\(In.RESOURCE.Name).go"
		}
	]], 1)

	...
}

